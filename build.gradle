plugins {
    id 'java'
    id 'org.jreleaser' version '1.15.0'
}

group = 'org.xcepto'
version = System.getenv("VERSION") ?: "1.0.0"

subprojects {
    group = 'org.xcepto'
    version = System.getenv("VERSION") ?: "1.0.0"
    apply plugin: 'java'


    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(21))
    }

    repositories {
        mavenCentral()
    }

    tasks.named("test") {
        finalizedBy(rootProject.tasks.named("composeDownAfter"))
    }
}

tasks.register("composeDownAfter") {
    doFirst {
        exec {
            commandLine("docker", "compose", "logs")
        }
    }
    doLast {
        exec {
            commandLine("docker", "compose", "down")
        }
    }
}



jreleaser {
    project {
        copyright = 'Copyright Â© 2025 themassiveone, Xcepto'
        description = 'BDD testing framework for distributed systems'
        java {
            multiProject = true
        }
    }
    signing {
        active = 'ALWAYS'
        armored = true
        mode = 'MEMORY'
        publicKey = System.getenv("JRELEASER_GPG_PUBLIC_KEY")
        secretKey = System.getenv("JRELEASER_GPG_SECRET_KEY")
        passphrase = System.getenv("JRELEASER_GPG_PASSPHRASE")
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    namespace = 'org.xcepto'
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                    password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
    release {
        github {
            enabled = false
        }
    }
}