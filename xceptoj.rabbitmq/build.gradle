
plugins{
    id 'java'
    id 'maven-publish'
}

group = 'org.xcepto'
version = System.getenv("VERSION") ?: "1.0.0"

dependencies {
//    if (System.getenv("VERSION")) {
//        implementation "org.xcepto:xceptoj:${System.getenv("VERSION")}"
//    } else {
//        implementation project(":xceptoj")
//    }
    implementation project(":xceptoj")
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'com.rabbitmq:amqp-client:5.25.0'
    implementation 'jakarta.inject:jakarta.inject-api:2.0.1'
    implementation 'io.github.resilience4j:resilience4j-retry:2.3.0'
}


java {
    withJavadocJar()
    withSourcesJar()
}


publishing {
    publications {
        rabbitmq(MavenPublication) {
            artifactId = 'xceptoj-rabbitmq'
            from components.java
            pom {
                name = 'xceptoj-rabbitmq'
                description = 'Declarative BDD framework for distributed systems'
                url = 'https://github.com/xcepto/xceptoj'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/license/mit'
                    }
                }
                developers {
                    developer {
                        id = 'themassiveone'
                        name = 'themassiveone'
                        email = 'me@themassiveone.net'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/xcepto/xceptoj.git'
                    developerConnection = 'scm:git:ssh://github.com:xcepto/xceptoj.git'
                    url = 'https://github.com/xcepto/xceptoj'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'staging'
            url = uri("${rootProject.buildDir}/staging-deploy")
        }
    }
}


tasks.withType(Jar) { task ->
    task.doLast {
        ant.checksum algorithm: 'md5', file: it.archivePath
        ant.checksum algorithm: 'sha1', file: it.archivePath
    }
}